CREATE PROCEDURE [HRSC_APP].[usp_Employee_GetUserRoleBCWithInterventionsCountByStatus]
	@pBCID bigint
WITH EXEC AS CALLER
AS
  SELECT E.EMPLOYEE_ID AS Id, E.EMPLOYEE_EMAIL_ADDRESS AS EmailAddress, E.EMPLOYEE_GIVEN_NAME AS GivenName, E.EMPLOYEE_SURNAME AS Surname, 
         E.WORK_PHONE_NUMBER AS WorkPhoneNumber, L.LANGUAGE_CODE2 AS LanguageCode, E.DISPLAY_NAME as DisplayName,
		 ET.TotalLeadActives, ET.TotalCollActives, ET.TotalLeadPending, ET.TotalCollPending
  FROM
  (SELECT ERBC.EMPLOYEE_ID,
   SUM(CASE WHEN ER.EMPLOYEE_ROLE_CODE = 'LEAD' AND RS.REQUEST_STATUS_CODE IN ('PROC', 'ASGN', 'RTNFC', 'SIGND') THEN 1 ELSE 0 END) TotalLeadActives,
   SUM(CASE WHEN ER.EMPLOYEE_ROLE_CODE = 'COL' AND RS.REQUEST_STATUS_CODE IN ('PROC', 'ASGN', 'RTNFC', 'SIGND') THEN 1 ELSE 0 END) TotalCollActives,
   SUM(CASE WHEN ER.EMPLOYEE_ROLE_CODE = 'LEAD' AND RS.REQUEST_STATUS_CODE IN ('WAIT', 'RTRNC', 'WSIGN') THEN 1 ELSE 0 END) TotalLeadPending,
   SUM(CASE WHEN ER.EMPLOYEE_ROLE_CODE = 'COL' AND RS.REQUEST_STATUS_CODE IN ('WAIT', 'RTRNC', 'WSIGN') THEN 1 ELSE 0 END) TotalCollPending
   FROM 
   (SELECT URBC.EMPLOYEE_ID
	   FROM 
	   [HRSC].[HRSC_USER_ROLE_BC] URBC
	   INNER JOIN [HRSC].[CD_HRSC_USER_ROLE] UR ON URBC.HRSC_USER_ROLE_ID = UR.HRSC_USER_ROLE_ID
	   INNER JOIN [HRSC].[CD_BUSINESS_CENTER] BC ON URBC.BUSINESS_CENTER_ID = BC.BUSINESS_CENTER_ID
	   INNER JOIN [HRSC].[EMPLOYEE] E ON URBC.EMPLOYEE_ID = E.EMPLOYEE_ID
	   WHERE URBC.BUSINESS_CENTER_ID = @pBCID AND 
		  UR.HRSC_USER_ROLE_CODE IN ('ADMIN', 'BCM', 'BCE') AND
		  URBC.EXPIRY_DATE IS NULL AND BC.EXPIRY_DATE IS NULL AND E.EXPIRY_DATE IS NULL) AS ERBC
   LEFT OUTER JOIN [HRSC].[HR_RQST_INTRVNT_HRSC_USER] IER ON ERBC.EMPLOYEE_ID = IER.EMPLOYEE_ID
   LEFT OUTER JOIN [HRSC].[HR_REQUEST_INTERVENTION] RI ON IER.REQUEST_INTERVENTION_ID = RI.HR_REQUEST_INTERVENTION_ID
   LEFT OUTER JOIN [HRSC].[CD_EMPLOYEE_ROLE] ER ON IER.EMPLOYEE_ROLE_ID = ER.EMPLOYEE_ROLE_ID
   LEFT OUTER JOIN [HRSC].[CD_REQUEST_STATUS] RS ON RI.REQUEST_STATUS_ID = RS.REQUEST_STATUS_ID
   GROUP BY ERBC.[EMPLOYEE_ID]) AS ET
   INNER JOIN [HRSC].[EMPLOYEE] E ON ET.EMPLOYEE_ID = E.EMPLOYEE_ID
   INNER JOIN [HRSC].[CD_LANGUAGE] L ON E.LANGUAGE_ID = L.LANGUAGE_ID
   ORDER BY E.EMPLOYEE_SURNAME, E.EMPLOYEE_GIVEN_NAME
